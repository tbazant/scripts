#!/usr/bin/perl

use strict;
use warnings;
use Data::Dumper;
use Getopt::Long;
use File::Basename;
use Pod::Usage;

my $man = 0;
my $help = 0;

our $xml_id = $ARGV[0];
our $debug = '';
our $path = 'xml/';

GetOptions (
  'help|?' => \$help,
  'debug' => \$debug,
  'xml-id=s' => \$xml_id,
  'path:s' => \$path,
) or pod2usage(2);
pod2usage(1) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

our %result = ();
our %lnums = ();
our @kid_arr = ();
our @new_order = ();

# assemble the initial cmd, run it
# and iterate over returned results recursively
my $cmd = "grep -n 'xml:id=\"" . $xml_id . "\"' " . $path . "*.xml";
debug($cmd);

foreach (qx($cmd)) {
  debug($_);
  my $extract = extract_filename($_);
  $lnums{$extract->{fname}} = $extract->{lnum};
  recurse($extract);
}

debug("result hash: \n");
print Dumper(%result) if $debug;

foreach (get_toplevels(\%result)) {
  @kid_arr = ();
  my @arr = ($_);
  push (@arr, recurse_kids($result{$_}));
  push (@new_order, \@arr);
}

foreach my $row (@new_order) {
  print $xml_id . ": ";
  print join(' -> ', @{$row});
  print "\n";
}

sub get_toplevels {
  my $result = shift;
  my @toplevels = @_;
  my @keys = keys(%{$result});
  my @vals = values(%{$result});
  foreach my $key (@keys) {
    unless (grep( /^$key$/, @vals) ) {
      debug("toplevel: $key" );
      push (@toplevels, $key);
    }
  }
  return @toplevels;
}

sub recurse_kids {
  my $kid = shift;
  if (grep(/^$kid$/, values(%result))) {
    push(@kid_arr, $path . $kid . ' +'. $lnums{$kid});
    if(exists($result{$kid})) {
      recurse_kids($result{$kid});
    } else {
      return @kid_arr;
    }
  }
}

# find which files are referring the file sent as an argument
# and for each of these file, do the same recursively
# and store the line-ups in a resulting hash
# intil no referrer is found
sub recurse {
  my $opts = shift;
  my $old_file = $opts->{fname};
  debug('old file: ' . $old_file);
  my $cmd = "grep -n 'href=\"$old_file\"' " . $path . "*.xml | egrep -v \"(link|xml-stylesheet|xml-model)\"";
  debug($cmd);
  my @lines = qx($cmd);
  foreach (@lines) {
    my $extract = extract_filename($_);
    $result{$extract->{fname}} = $old_file;
    $lnums{$extract->{fname}} = $extract->{lnum};
    recurse($extract);
  }
}

# get a grep line and return a file name + the line number
sub extract_filename {
  my $line = shift;
  my @arr = split(':', $line);
  my $path = $arr[0];
  my $lnum = $arr[1];
  my ($fname, $dirs) = fileparse($path);
  my $result = {
    'fname' => $fname,
    'lnum' => $lnum,
  };
  return $result;
}

sub debug {
  my $msg = shift;
  chomp($msg);
  print STDERR "DEBUG: $msg\n" if $debug;
}

__END__
=head1 NAME

get_book_from_id - find where the given XML ID belongs in a docbook structure

=head1 SYNOPSIS

get_book_from_id [options]

=head1 OPTIONS

=over 8

=item B<-h or --help>

Print a brief help message and exits.

=item B<-d or --debug>

Print detailed debug messages.

=item B<-x or --xml-id>

XML ID to be examined for its ancestors.

=item B<-p or --path>

Path where DocBook XML files are stored. Default is 'xml/'.


=back

=cut
